// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.5.0'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

task startLocalServer (dependsOn: ":backend:appengineRun") {
    project(":backend").afterEvaluate { backend ->
        backend.extensions.appengine.daemon = true
    }
    doLast {
        println "Local server started"
    }
}

task runAndroidTest {
    dependsOn ":app:connectedCheck"
    doLast {
        println "Android tests finished"
    }
}

task stopLocalServer {
    dependsOn ":backend:appengineStop"
    doLast {
        println "Local server stopped"
    }
}

task fullTest {
    dependsOn = ['startLocalServer', 'runAndroidTest', 'stopLocalServer']
}

runAndroidTest.mustRunAfter startLocalServer
runAndroidTest.finalizedBy stopLocalServer
